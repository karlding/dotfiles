set nocompatible              " be iMproved, required
filetype off                  " required

" Plugins {{{
" Define bundles via GitHub repositories
call plug#begin('~/.vim/plugged')
  " General {{{
    Plug 'sgur/vim-editorconfig'
  " }}}

  " Syntax {{{
    Plug 'vim-scripts/jade.vim', { 'for': ['jade'], 'tag': '1.1' }
    Plug 'rust-lang/rust.vim', { 'for': ['rust'] }
    Plug 'saltstack/salt-vim', { 'for': ['sls'] }
    Plug 'elixir-lang/vim-elixir', { 'for': ['elixir'] }
    Plug 'fatih/vim-go', { 'for': ['go'], 'tag': 'v1.14' , 'do': ':GoInstallBinaries' }
    Plug 'vim-ruby/vim-ruby', { 'for': ['ruby'], 'tag': 'stable-20160928' }
    Plug 'cespare/vim-toml', { 'for': ['toml'] }
  " }}}

  " UI/UX {{{
    Plug 'chriskempson/base16-vim'
    Plug 'scrooloose/nerdtree', { 'on': 'NERDTreeToggle' , 'tag': '5.0.0' }
    Plug 'airblade/vim-gitgutter'
    Plug 'christoomey/vim-tmux-navigator', { 'tag': 'v1.0' }
  " }}}

  " Miscellaneous {{{
    Plug 'shougo/neocomplete.vim'
    Plug 'shougo/neoinclude.vim'
    Plug 'vim-syntastic/syntastic', { 'on': 'SyntasticCheck', 'tag': '3.8.0' }
    Plug 'godlygeek/tabular', { 'tag': '1.0.0' }
    Plug 'mbbill/undotree', { 'tag': 'rel_5.0' }
    Plug 'rhysd/vim-clang-format', { 'for': ['c', 'cpp', 'proto'] }
  " }}}
call plug#end()
" }}}


" Plugin Configuration {{{
  " Plugin: vim-base16 {{{
    if filereadable(expand("~/.vimrc_background"))
      let base16colorspace=256  " Access colors present in 256 colorspace
      source ~/.vimrc_background
    endif
  " }}}

  " Plugin: vim-gitgutter {{{
    let g:gitgutter_realtime = 0    " disable realtime updates on Stop typing event
    let g:gitgutter_eager = 0       " disable eager updates
  " }}}

  " Plugin: vim-go {{{
    let g:go_highlight_functions = 1
    let g:go_highlight_methods = 1
    let g:go_highlight_structs = 1
    let g:go_highlight_interfaces = 1
    let g:go_highlight_operators = 1
    let g:go_highlight_build_constraints = 1

    autocmd FileType go set number fo+=croq tw=100
    autocmd Filetype go set makeprg=go\ build\ .
  " }}}

  " Plugin: neocomplete {{{
    " Disable AutoComplPop.
    let g:acp_enableAtStartup = 0

    " Use neocomplete
    let g:neocomplete#enable_at_startup = 1
    " use camel case
    let g:neocomplete#enable_camel_case = 1
    " use smartcase
    let g:neocomplete#enable_smart_case = 1

    " Default # of completions is 100, that's crazy.
    let g:neocomplete#max_list = 5

    " Set minimum syntax keyword length
    let g:neocomplete#auto_completion_start_length = 3

    " This makes sure we use neocomplete completefunc instead of
    " the one in rails.vim, otherwise this plugin will crap out.
    let g:neocomplete#force_overwrite_completefunc = 1

    " Define keyword.
    if !exists('g:neocomplete#keyword_patterns')
      let g:neocomplete#keyword_patterns = {}
    endif
    let g:neocomplete#keyword_patterns['default'] = '\h\w*'

    " key-mappings
    inoremap <expr><C-g>    neocomplete#undo_completion()
    inoremap <expr><C-l>    neocomplete#complete_common_string()
    " <CR>: close pop-up and save indent
    inoremap <silent> <CR> <C-r>=<SID>my_cr_function()<CR>
    function! s:my_cr_function()
        return neocomplete#close_popup() . "\<CR>"
        " For no inserting <CR> key
        return pumvisible() ? neocomplete#close_popup() : "\<CR>"
    endfunction
    " <TAB>: completion
    inoremap <expr><TAB>  pumvisible() ? "\<C-n>" : "\<TAB>"
  " }}}

  " Plugin: ag.vim {{{
    " When you press gv you Ag after the selected text
    vnoremap <silent> gv :call VisualSelection('gv', '')<CR>

    " Open Ag and put the cursor in the right position
    map <leader>g :Ag

    " When you press <leader>r you can search and replace the selected text
    vnoremap <silent> <leader>r :call VisualSelection('replace', '')<CR>

    " Do :help cope if you are unsure what cope is. It's super useful!
    "
    " When you search with Ag, display your results in cope by doing:
    "   <leader>cc
    "
    " To go to the next search result do:
    "   <leader>n
    "
    " To go to the previous search results do:
    "   <leader>p
    "
    map <leader>cc :botright cope<cr>
    map <leader>co ggVGy:tabnew<cr>:set syntax=qf<cr>pgg
    map <leader>n :cn<cr>
    map <leader>p :cp<cr>
  " }}}
" }}}

" Enable omni completion
autocmd FileType css setlocal omnifunc=csscomplete#CompleteCSS
autocmd FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
autocmd FileType javascript setlocal omnifunc=javascriptcomplete#CompleteJS
autocmd FileType python setlocal omnifunc=pythoncomplete#Complete
autocmd FileType xml setlocal omnifunc=xmlcomplete#CompleteTags
autocmd FileType ruby setlocal omnifunc=rubycomplete#Complete


" Sets how many lines of history VIM has to remember
set history=500
set textwidth=80
set colorcolumn=+1


" Spacing {{{
  set smarttab                        " Be smart when using tabs ;)
  set expandtab                       " Use spaces instead of spaces
  filetype plugin indent on           " Load filetype-specific indentation

  " 1 tab == 4 spaces
  set shiftwidth=4
  set tabstop=4
" }}}

" Commands {{{
  " :W sudo saves the file
  " (useful for handling the permission-denied error)
  command W w !sudo tee % > /dev/null
" }}}

" Autocomplete {{{
  set wildmenu                                " Turn on the WiLd menu
  set wildmode=list:full

  " Compiled object files
  set wildignore=*.o,*.obj,*.exe,*.dll,*~,*.pyc
  " Version Control
  if has("win16") || has("win32")
      set wildignore+=*/.git/*,*/.hg/*,*/.svn/*,*/.DS_Store
  else
      set wildignore+=.git\*,.hg\*,.svn\*
  endif
  " LaTeX intermediate files
  set wildignore+=*.aux,*.out,*.toc
  " Images
  set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg
  " Compiled spelling lists
  set wildignore+=*.spl
" }}}

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => VIM user interface
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Set 7 lines to the cursor - when moving vertically using j/k
set so=7

" Avoid garbled characters in Chinese language windows OS
let $LANG='en'
set langmenu=en
source $VIMRUNTIME/delmenu.vim
source $VIMRUNTIME/menu.vim

" Height of the command bar
set cmdheight=2

" A buffer becomes hidden when it is abandoned
set hid

" Configure backspace so it acts as it should act
set backspace=eol,start,indent

set whichwrap+=<,>,h,l


" Search {{{
  set gdefault      " Default global search flag
  set ignorecase    " Ignore case when searching
  set incsearch     " Dynamic highlighting
  set smartcase     " When searching, try to be smart about cases
  set hlsearch      " Highlight search results
  set magic         " For regular expressions turn magic on
" }}}

" Folding {{{
  set foldenable            " Enable folds
  set foldlevelstart=10     " Open most folds by default
  set foldnestmax=7         " 8 nested fold max
  set foldmethod=syntax     " Fold based on file syntax
" }}}


" How many tenths of a second to blink when matching brackets
set mat=2

" No annoying sound on errors
set noerrorbells
set novisualbell
set t_vb=
set tm=500


" Theme {{{
  set background=dark

  " Enable syntax highlighting
  if !exists("g:syntax_on")
      syntax enable
  endif

  try
      colorscheme base16-default-dark
  endtry
" }}}


" Set extra options when running in GUI mode
if has("gui_running")
    set guioptions-=T
    set guioptions-=e
    set t_Co=256
    set guitablabel=%M\ %t
endif

" Set utf8 as standard encoding and en_US as the standard language
set encoding=utf8

" Use Unix as the standard file type
set ffs=unix,dos,mac


" Undo/Backup/Swap {{{
  if !isdirectory(expand("~/.vim/undo/"))
    silent !mkdir -p ~/.vim/undo
  endif

  set nobackup              " No backup files
  set nowritebackup         " No backup files while editing
  set noswapfile            " No swap file

  " Undotree {{{
    set undodir^=~/.vim/undo/                  " Directory to put undo files
    set undofile
  " }}}
" }}}


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Text, tab and indent related
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Linebreak on 500 characters
set lbr
set tw=500

set ai "Auto indent
set si "Smart indent
set wrap "Wrap lines


" Mappings {{{
  " With a map leader it's possible to do extra key combinations
  let mapleader = ","
  let g:mapleader = ","

  " Disable <F1> for :help
  nnoremap <F1> <nop>

  " tbh who actually uses q:
  nnoremap q: :q

  " Visual mode pressing * or # searches for the current selection
  " Super useful! From an idea by Michael Naumann
  vnoremap <silent> * :call VisualSelection('f', '')<CR>
  vnoremap <silent> # :call VisualSelection('b', '')<CR>

  " Treat long lines as break lines (useful when moving around in them)
  nnoremap j gj
  nnoremap k gk
  nnoremap J gj
  nnoremap K gk

  " Map <Space> to / (search) and Ctrl-<Space> to ? (backwards search)
  nnoremap <space> /
  nnoremap <c-space> ?

  " Disable highlight when <leader><cr> is pressed
  nnoremap <silent> <leader><cr> :noh<cr>

  " Fast saving
  nnoremap <leader>w :w!<cr>

  " Smart way to move between windows
  nnoremap <C-j> <C-W>j
  nnoremap <C-k> <C-W>k
  nnoremap <C-h> <C-W>h
  nnoremap <C-l> <C-W>l

  " Close the current buffer
  nnoremap <leader>bd :Bclose<cr>:tabclose<cr>gT

  " Close all the buffers
  nnoremap <leader>ba :bufdo bd<cr>

  " Easier buffer navigation
  nnoremap <Tab> :bnext<cr>
  nnoremap <S-Tab> :bprevious<cr>

  " Useful mappings for managing tabs
  map <leader>tn :tabnew<cr>
  map <leader>to :tabonly<cr>
  map <leader>tc :tabclose<cr>
  map <leader>tm :tabmove
  map <leader>t<leader> :tabnext

  " Let 'tl' toggle between this and the last accessed tab
  let g:lasttab = 1
  nmap <Leader>tl :exe "tabn ".g:lasttab<CR>
  au TabLeave * let g:lasttab = tabpagenr()

  " Spell-checking {{{
    " Pressing ,ss will toggle and untoggle spell checking
    map <leader>ss :setlocal spell!<cr>

    " Shortcuts using <leader>
    " next spelling error
    map <leader>sn ]s
    " previous spelling error
    map <leader>sp [s
    " add word to spelling dictionary
    map <leader>sa zg
    " view spelling correction suggestions
    map <leader>s? z=
  " }}}
  " Plugin: Undotree {{{
    nnoremap <Leader>u :UndotreeToggle<CR>
    " If undotree is opened, it is likely one wants to interact with it.
    let g:undotree_SetFocusWhenToggle=1
  " }}}
" }}}

" Opens a new tab with the current buffer's path
" Super useful when editing files in the same directory
map <leader>te :tabedit <c-r>=expand("%:p:h")<cr>/

" Switch CWD to the directory of the open buffer
map <leader>cd :cd %:p:h<cr>:pwd<cr>

" Specify the behavior when switching between buffers
try
  set switchbuf=useopen,usetab,newtab
  set stal=2
catch
endtry


" Status line {{{
  set laststatus=2                              " Always show the status line

  " Format the status line
  set statusline=\ %{HasPaste()}
  set statusline+=%F                            " Full path to the file in the buffer
  set statusline+=%m                            " Modified flag
  set statusline+=%r                            " Read-only flag
  set statusline+=%h                            " Help buffer flag
  set statusline+=\ %w

  set statusline+=\ \ cwd:\ %{getcwd()}         " Current working directory

  set statusline+=\ \ \ Line:\ %l               " Line number
  set statusline+=\/%L                          " Number of lines in buffer

  set statusline+=\ \ Column:\ %c               " Column number
" }}}


" Delete trailing white space on save, useful for Python and CoffeeScript ;)
func! DeleteTrailingWS()
  exe "normal mz"
  %s/\s\+$//ge
  exe "normal `z"
endfunc
autocmd BufWrite *.py :call DeleteTrailingWS()
autocmd BufWrite *.coffee :call DeleteTrailingWS()



"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Misc
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Remove the Windows ^M - when the encodings gets messed up
noremap <Leader>m mmHmt:%s/<C-V><cr>//ge<cr>'tzt'm


" Toggle paste mode on and off
map <leader>pp :setlocal paste!<cr>



" Helper Functions {{{
  function! CmdLine(str)
      exe "menu Foo.Bar :" . a:str
      emenu Foo.Bar
      unmenu Foo
  endfunction

  function! VisualSelection(direction, extra_filter) range
      let l:saved_reg = @"
      execute "normal! vgvy"

      let l:pattern = escape(@", '\\/.*$^~[]')
      let l:pattern = substitute(l:pattern, "\n$", "", "")

      if a:direction == 'b'
          execute "normal ?" . l:pattern . "^M"
      elseif a:direction == 'gv'
          call CmdLine("Ag \"" . l:pattern . "\" " )
      elseif a:direction == 'replace'
          call CmdLine("%s" . '/'. l:pattern . '/')
      elseif a:direction == 'f'
          execute "normal /" . l:pattern . "^M"
      endif

      let @/ = l:pattern
      let @" = l:saved_reg
  endfunction

  " Returns true if paste mode is enabled
  function! HasPaste()
      if &paste
          return 'PASTE MODE  '
      endif
      return ''
  endfunction

  " Don't close window, when deleting a buffer
  command! Bclose call <SID>BufcloseCloseIt()
  function! <SID>BufcloseCloseIt()
     let l:currentBufNum = bufnr("%")
     let l:alternateBufNum = bufnr("#")

     if buflisted(l:alternateBufNum)
       buffer #
     else
       bnext
     endif

     if bufnr("%") == l:currentBufNum
       new
     endif

     if buflisted(l:currentBufNum)
       execute("bdelete! ".l:currentBufNum)
     endif
  endfunction
" }}}

" Languages {{{
  " Python {{{
    let python_highlight_all = 1
    au FileType python syn keyword pythonDecorator True None False self

    au BufNewFile,BufRead *.jinja set syntax=htmljinja
    au BufNewFile,BufRead *.mako set ft=mako

    au FileType python map <buffer> F :set foldmethod=indent<cr>

    au FileType python inoremap <buffer> $r return
    au FileType python inoremap <buffer> $i import
    au FileType python inoremap <buffer> $p print
    au FileType python inoremap <buffer> $f #--- PH ----------------------------------------------<esc>FP2xi
    au FileType python map <buffer> <leader>1 /class
    au FileType python map <buffer> <leader>2 /def
    au FileType python map <buffer> <leader>C ?class
    au FileType python map <buffer> <leader>D ?def
  " }}}

  " JavaScript {{{
    au FileType javascript setl fen
    au FileType javascript setl nocindent

    au FileType javascript imap <c-t> $log();<esc>hi
    au FileType javascript imap <c-a> alert();<esc>hi

    au FileType javascript inoremap <buffer> $r return
    au FileType javascript inoremap <buffer> $f //--- PH ----------------------------------------------<esc>FP2xi
  " }}}

  " gitcommit {{{
    au FileType gitcommit call setpos('.', [0, 1, 1, 0])
  " }}}
" }}}

" Miscellaneous {{{
  set lazyredraw    " Don't redraw while executing macros
  set showmatch     " Highlight matching brackets
  set autoread      " Read when a file is changed from the outside
  set shortmess+=I  " Remove startup message

  " In many terminal emulators the mouse works just fine, thus enable it.
  if has('mouse')
    set mouse=a
  endif
" }}}

" Machine-specific local config {{{
  if filereadable(expand("~/.vimrc.local"))
    source ~/.vimrc.local
  endif
" }}}

" vim:foldmethod=marker:foldlevel=0
